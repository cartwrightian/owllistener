
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.4.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'nebula.deb'

mainClassName = 'tw.com.owllistener.network.Run'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

repositories {
    mavenCentral()
}

configurations {
	compileOnly
	testRuntime.extendsFrom(compileOnly)
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.5',
            'ch.qos.logback:logback-classic:1.0.10',
            'org.apache.commons:commons-csv:1.1',
            'org.glassfish.jersey.core:jersey-client:2.25.1',
            'com.typesafe:config:1.3.1'

    testCompile 'junit:junit:4.11',
            'org.easymock:easymock:3.2',
            'com.github.tomakehurst:wiremock:2.6.0'
}

sourceSets {
	main { java { srcDirs 'src/main' } }
	test { java { srcDirs 'src/test' } }
}

sourceSets.all {
    compileClasspath += configurations.compileOnly
}

test {
    useJUnit {
        excludeCategories 'tw.com.owllistener.integration.IntegrationTest'
    }
}

task integration(type: Test) {
    useJUnit {
        includeCategories 'tw.com.owllistener.integration.IntegrationTest'
    }
}

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' '),
                "Main-Class": mainClassName
        )
    }
}

task debianPackage(type: Deb) {
    packageName = 'owllistener'
    release = 1
    version = '0.1'
    os = LINUX

    configurationFile('/etc/defaults/owllistener')

    requires('openjdk-8-jre-headless')

    into '/opt/owllistener'
    from(configurations.runtime) {
        into 'lib'
    }
    from(jar.outputs.files) {
        into 'lib'
    }
    from('src/main/resources') {
        into 'conf'
    }
    from('scripts') {
        into 'bin'
        fileMode 0755
    }
    from('etc') {
        into 'etc'
    }
    link('/etc/init.d/owllistener', '/opt/owllistener/bin/owllistener.sh')

}
debianPackage.dependsOn assembleDist




